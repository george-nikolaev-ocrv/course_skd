Перем МакетКомпоновки;

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	Если ТипЗнч(ДокументРезультат) = Тип("ТабличныйДокумент") Тогда
		ТипГенератора = Тип("ГенераторМакетаКомпоновкиДанных");
	Иначе
		ТипГенератора = Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений");
	КонецЕсли;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки()
		,,, ТипГенератора);
	
	ВывестиТекстЗапроса();
	
	Если ЕстьОбъектДанных() Или
		ТипЗнч(ТипГенератора) = Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений") Тогда
		СтандартнаяОбработка = Ложь;
		ПолучитьОбъектыДанных(ДокументРезультат, ДанныеРасшифровки);
	КонецЕсли;
КонецПроцедуры

Процедура ВывестиТекстЗапроса()
	
	Для Каждого Набор Из МакетКомпоновки.НаборыДанных Цикл
		Если ТипЗнч(Набор) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			Сообщить(Набор.Запрос);
		КонецЕсли;
				
	КонецЦикла;
	
	
КонецПроцедуры

Функция ЕстьОбъектДанных()
	Для Каждого Набор Из МакетКомпоновки.НаборыДанных Цикл
		Если ТипЗнч(Набор) = Тип("НаборДанныхОбъектМакетаКомпоновкиДанных") Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат Ложь;
КонецФункции

Процедура ПолучитьОбъектыДанных(ДокументРезультат, ДанныеРасшифровки)
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных());
	
	Если ТипЗнч(ДокументРезультат) = Тип("ТабличныйДокумент") Тогда
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент();
		ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	Иначе
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений();
		ПроцессорВывода.УстановитьОбъект(ДокументРезультат);
	КонецЕсли;
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
КонецПроцедуры

Функция ВнешниеНаборыДанных()
	Наборы = Новый Структура;
	Для Каждого Набор Из МакетКомпоновки.НаборыДанных Цикл
		Если ТипЗнч(Набор) = Тип("НаборДанныхОбъектМакетаКомпоновкиДанных") Тогда
			Наборы.Вставить(Набор.ИмяОбъекта, Вычислить(Набор.ИмяОбъекта + "()"));
		КонецЕсли;
	КонецЦикла;
	Возврат Наборы;
КонецФункции 

Функция ТаблицаЦен()
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("КодТовара");
	Таблица.Колонки.Добавить("Цена");
	
	Данные = Таблица.Добавить();
	Данные.КодТовара = "000000001";
	Данные.Цена = "10.5";
	
	Данные = Таблица.Добавить();
	Данные.КодТовара = "000000002";
	Данные.Цена = "50.45";
	
	Возврат Таблица;
КонецФункции