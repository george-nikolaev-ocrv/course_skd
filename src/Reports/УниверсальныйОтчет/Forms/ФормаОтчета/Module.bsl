&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ОбъектДанных.СписокВыбора.ЗагрузитьЗначения(ДоступныеОбъектыДанных());
КонецПроцедуры

&НаСервере
Функция ДоступныеОбъектыДанных()
	ОбъектыДанных = Новый Массив;
	Для Каждого Элемент Из Метаданные.Справочники Цикл
		ОбъектыДанных.Добавить(Элемент.ПолноеИмя());		
	КонецЦикла;
	Возврат ОбъектыДанных;
КонецФункции

&НаКлиенте
Процедура ОбъектДанныхПриИзменении(Элемент)
	Элементы.ИмяСхемы.СписокВыбора.ЗагрузитьЗначения(ОбъектДанныхПриИзмененииНаСервере(Отчет.ОбъектДанных));
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбъектДанныхПриИзмененииНаСервере(ИмяОбъектаДанных)
	Макеты = Новый Массив;
	ОбъектДанных = Метаданные.НайтиПоПолномуИмени(ИмяОбъектаДанных);
	Для Каждого Элемент Из ОбъектДанных.Макеты Цикл
		Макеты.Добавить(Элемент.Имя);
	КонецЦикла;
	Возврат Макеты;
КонецФункции

&НаКлиенте
Процедура ВариантОтчетаПриИзменении(Элемент)
	УстановитьСхему(Отчет.ИмяСхемы);
КонецПроцедуры

&НаСервере
Процедура УстановитьСхему(ИмяСхемы)
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	Менеджер = Новый (СтрЗаменить(Отчет.ОбъектДанных, ".", "Менеджер."));
	ОтчетОбъект.СхемаКомпоновкиДанных = Менеджер.ПолучитьМакет(ИмяСхемы);
	ЗначениеВРеквизитФормы(ОтчетОбъект, "Отчет");
	
КонецПроцедуры

